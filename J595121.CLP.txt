/*-------------------------------------------------------------------*/         
/*  Program: J595121                                                 */         
/*     Text: Print Can Labels - Write Signal File to ChemGes         */         
/*     Date: 03.02.2007                                              */         
/*   Author: Charlie McLean                                          */         
/*  Project: S10014553                                               */         
/*                                                                   */         
/*-------------------------------------------------------------------*/         
/* Function: Print Can Labels - Write Signal File to ChemGes         */         
/*                                                                   */         
/*-------------------------------------------------------------------*/         
/* Indicator/Function                                                */         
/* &IN03     Exit (F01 THRU F24, reserved)                           */         
/* &IN51     Display pgmmsgq subfile                                 */         
/*                                                                   */         
/*-------------------------------------------------------------------*/         
/*           Modifications                                           */         
/*-------------------------------------------------------------------*/         
/* S10014553 03.15.2007  Charlie McLean                              */         
/*           ------------------------------------------------------- */         
/*           Added .DAT to signal file.                              */         
/*                                                                   */         
/* S10014553 03.26.2007  Charlie McLean                              */         
/*           ------------------------------------------------------- */         
/*           Chg'd FTP server to d:\chemges and added CD watch cmd   */         
/*                                                                   */         
/* S10014553 06.01.2007  Charlie McLean                              */         
/*           ------------------------------------------------------- */         
/*           Suppress msgs from FTP process.                         */         
/*                                                                   */         
/* CC25921   06.02.2008  Charlie McLean                              */         
/*           ------------------------------------------------------- */         
/*           Use PROCESS03 for jobq.                                 */         
/*                                                                   */         
/*-------------------------------------------------------------------*/         
/*           Begin Program                                           */         
/*-------------------------------------------------------------------*/         
 START:      PGM        PARM(&STRNG)                                            
                                                                                
/*-------------------------------------------------------------------*/         
/*           Variables                                               */         
/*-------------------------------------------------------------------*/         
             DCL        &STRNG  *CHAR LEN(128)                                  
             DCL        &USER   *CHAR LEN(10)                                   
             DCL        &USER3  *CHAR LEN(3)                                    
             DCL        &NBR     *CHAR LEN(6)                                   
             DCL        &SIGNAL  *CHAR LEN(10)                                  
             DCL        &SIGNAL2 *CHAR LEN(14)                                  
             DCL        VAR(&FTPSITE) TYPE(*CHAR) LEN(10) +                     
                          VALUE('sersvr015')                                    
             DCL        &JOBTYPE   *CHAR   1 /* 1=interactive 0=batch*/         
                                                                                
/*-------------------------------------------------------------------*/         
/*           Program Monitor Messages                                */         
/*-------------------------------------------------------------------*/         
             MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(STDERR))                
                                                                                
/*-------------------------------------------------------------------*/         
/*           Mainline Program                                        */         
/*-------------------------------------------------------------------*/         
             CHGJOB     LOG(4 99 *SECLVL) LOGCLPGM(*YES)                        
/*           CHGJOB     LOG(0 0 *NOLIST) LOGCLPGM(*NO)               */         
                                                                                
             RTVJOBA    USER(&USER) NBR(&NBR) TYPE(&JOBTYPE)                    
/*  If interactive job submit to batch                               */         
             IF         COND(&JOBTYPE = '1') THEN(DO)                           
             SBMJOB     CMD(CALL PGM(J595121) PARM(&STRNG)) +                   
                          JOBQ(PROCESS04)                                       
             GOTO       CMDLBL(EOJ)                                             
             ENDDO                                                              
             CHGVAR     VAR(&USER3) VALUE(%SST(&USER 1 3))                      
             CHGVAR     VAR(&SIGNAL) VALUE('F' *TCAT &NBR *TCAT &USER3)         
/*  ChemGes wants a file name with a .DAT extension                  */         
             CHGVAR     VAR(&SIGNAL2) VALUE('F' *TCAT &NBR *TCAT +              
                          &USER3 *TCAT '.dat')                                  
             DLTF       FILE(QTEMP/&SIGNAL)                                     
             MONMSG     MSGID(CPF2105) /* not found */                          
             CRTPF      FILE(QTEMP/&SIGNAL) RCDLEN(128) +                       
                          TEXT('ChemGes Signal File')                           
                                                                                
/* create Input source file and member for FTP                       */         
             DLTF       FILE(QTEMP/T595121)                                     
             MONMSG     MSGID(CPF2105)                                          
             CRTSRCPF   FILE(QTEMP/T595121) TEXT('FTP control member +          
                          file') AUT(*ALL)                                      
             ADDPFM     FILE(QTEMP/T595121) MBR(T595121)                        
/* depends on TAATOOL WRTSRC                                         */         
             WRTSRC     SRCFILE(QTEMP/T595121) MBR(T595121) +                   
                          STMT('as400ftp h4109rv9')                             
             WRTSRC     SRCFILE(QTEMP/T595121) MBR(T595121) +                   
                          STMT('ASCII')                                         
             WRTSRC     SRCFILE(QTEMP/T595121) MBR(T595121) +                   
                          STMT('LCD QTEMP')                                     
             WRTSRC     SRCFILE(QTEMP/T595121) MBR(T595121) STMT('CD +          
                          WATCH')                                               
             WRTSRC     SRCFILE(QTEMP/T595121) MBR(T595121) +                   
                          STMT('PUT ' || &SIGNAL || ' ' || &SIGNAL2)            
             WRTSRC     SRCFILE(QTEMP/T595121) MBR(T595121) +                   
                          STMT('QUIT')                                          
             WRTSRC     SRCFILE(QTEMP/T595121) MBR(T595121) +                   
                          ACTION(*END)    /* Close Source File       */         
                                                                                
             OVRDBF     FILE(F595121) TOFILE(QTEMP/&SIGNAL)                     
             CALL       PGM(P595121) PARM(&STRNG)                               
             OVRDBF     FILE(INPUT) TOFILE(*LIBL/T595121) MBR(T595121)          
             OVRDBF     FILE(OUTPUT) TOFILE(*LIBL/T595121) MBR(FTPLOG)          
             FTP        RMTSYS(&FTPSITE)                                        
/*           DLTOVR     FILE(*ALL)                                   */         
                                                                                
/*-------------------------------------------------------------------*/         
/*           Normal End of Program                                   */         
/*-------------------------------------------------------------------*/         
                                                                                
 EOJ:        RETURN                                                             
                                                                                
/*-------------------------------------------------------------------*/         
/*           Standard Error Handeling                                */         
/*-------------------------------------------------------------------*/         
 STDERR:                                                                        
             SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) +                          
                          MSGDTA('Program aborted on an expected +              
                          escape mesage.') MSGTYPE(*ESCAPE)                     
                                                                                
 END:        ENDPGM                                                             
